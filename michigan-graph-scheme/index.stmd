% Installing and Using the Michigan Graph Scheme
% Andy Grogan-Kaylor
% `s c(current_date)` `s c(current_time)`


---
geometry: margin=1 in
---

<style>body {max-width: 90%;} h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;} code {color: red;}</style>

# Introduction

```{s/}

    clear all

    quietly set obs 10

    generate x = _n

    graph bar (count), ///
	over(x, axis(off)) asyvars ///
	ytitle(" ") ylabel(none) ///
	yscale(lcolor(none)) ///
	title(Michigan Graph Scheme Colors) ///
	scheme(michigan) ///
	ysize(1.5) scale(2)
	
    quietly graph export MichiganColorsStata.png, width(750) replace

```

![Colors in Michigan Graph Scheme](MichiganColorsStata.png)

Stata provides the use of graph schemes that improve the overall look of graphs.

See `help scheme`.

The *Michigan graph scheme* makes use of official University of Michigan colors.

# Installation

Use of the *Michigan graph scheme* depends on installation of the `lean2' graph scheme developed by Svend Juul.

Type `findit lean2` and click through on the install links to install `lean2`.
      
Then type `net from https://agrogan1.github.io/Stata` and click the links to install.

# Example Data

We are going to use the famous "iris" data collected by Edgar Anderson.

```{s}
    clear all
	
	use "iris.dta", clear

	summarize
```

# Histogram

```{s}
    histogram Petal_Length, scheme(michigan)
```

```{s/}	
    quietly graph export myhistogram.png, width(750) replace
```
	
![Histogram Using Michigan Scheme](myhistogram.png)

# Histogram With Transparency

```{s}
    histogram Petal_Length, fcolor(%50) scheme(michigan)
```

```{s/}	
    quietly graph export myhistogram2.png, width(750) replace
```
	
![Histogram Using Michigan Scheme And Slightly Transparent Bars](myhistogram2.png)


# Bar Graph

```{s}
    graph bar Petal_Length, over(Species) scheme(michigan) asyvars
```

```{s/}	
    quietly graph export mybargraph.png, width(750) replace
```
	
![Bar Graph Using Michigan Scheme](mybargraph.png)

# Bar Graph With Transparency

```{s}
    graph bar Petal_Length, over(Species) intensity(70) scheme(michigan) asyvars
```

```{s/}
    quietly graph export mybargraph2.png, width(750) replace
```
	
![Bar Graph Using Michigan Scheme and Slightly Transparent Bars](mybargraph2.png)


# Scatterplot

```{s}
    twoway (scatter Petal_Length Petal_Width) ///
	(lfit Petal_Length Petal_Width), ///
	scheme(michigan)
```

```{s/}	
	quietly graph export myscatter.png, width(750) replace
```
	
![Scatterplot Using Michigan Scheme](myscatter.png)

# Scatterplot With Transparency

```{s}
    twoway (scatter Petal_Length Petal_Width, mcolor(%30)) /// markers have 30% transparency
	(lfit Petal_Length Petal_Width), ///
	scheme(michigan)
```

```{s/}	
	quietly graph export myscatter2.png, width(750) replace
```
	
![Scatterplot Using Michigan Scheme And Slightly Transparent Markers](myscatter2.png)

# Legend Placement

Sometimes you may wish to have the legend of the graph placed at the *bottom* of the graph. The `pos(6)` suboption inside the `legend` option will place the legend at the bottom, while you can manually control the number of legend rows with the `rows` suboption.

```{s}
    graph bar Petal_Length, over(Species) scheme(michigan) asyvars legend(pos(6) rows(1))
```

```{s/}
    quietly graph export mybargraph3.png, width(750) replace
```
	
![Bar Graph Using Michigan Scheme and Modified Legend](mybargraph3.png)

# Individual Michigan Colors

Individual University of Michigan colors are listed below. 

-------------------------------------------------------------
Color                Hex        RGB
-------------------- ---------- ----------
Blue                 #00274C    0 39 76

Maize                #FFCB05    255 203 5

Tappan Red           #9A3324    154 51 36

Ross School Orange   #D86018    216 96 24

Wave Field Green     #A5A508    165 165 8

Taubman Teal         #00B2A9    0 178 169

Arboretum Blue       #2F65A7    47 101 167

Ann Arbor Amethyst   #702082    112 32 130

Matthaei Violet      #575294    87 82 148

Umma Tan             #CFC096    207 192 150

Burton Tower Beige   #9B9A6D    155 154 109

Angell Hall Ash      #989C97    152 156 151

Law Quad Stone       #655A52    101 90 82
-------------------------------------------------------------

Stata can use RGB codes for colors. As an example.

```{s}
    twoway (scatter Petal_Length Petal_Width, mcolor("112 32 130 %30")) /// markers are Amethyst with 30% transparency
	(lfit Petal_Length Petal_Width, lcolor("87 82 148")), /// Violet line
	scheme(michigan)
```

```{s/}	
	quietly graph export myscatter3.png, width(750) replace
```

![Scatterplot Using Michigan Scheme, Selected Colors, And Slightly Transparent Markers](myscatter3.png)







